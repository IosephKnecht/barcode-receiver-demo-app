apply plugin: 'maven-publish'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'

    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from javadoc
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'

    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = 'https://github.com/IosephKnecht/barcode-receiver-demo-app'

                    licenses {
                        license {
                            name = 'The Unlicense'
                            url = 'https://github.com/IosephKnecht/barcode-receiver-demo-app/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'IosephKnecht'
                            name = 'Alexander Mezentsev'
                            email = 'dadyafedar@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/IosephKnecht/barcode-receiver-demo-app.git'
                        developerConnection = 'scm:git:ssh://github.com/IosephKnecht/barcode-receiver-demo-app.git'
                        url = 'https://github.com/IosephKnecht/barcode-receiver-demo-app/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}